apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.github.dcendents.android-maven'
group = rootProject.ext.group

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    def markusresselCommonsVersion = "master-SNAPSHOT"
    // choose the ones you need
    implementation("com.github.markusressel.commons:android_themes:${markusresselCommonsVersion}")

    api "androidx.annotation:annotation:$androidKtxVersion"
    implementation "androidx.appcompat:appcompat:$androidKtxVersion"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.core:core-ktx:1.0.0"

    implementation "com.google.code.gson:gson:2.8.5"
    implementation('com.github.markusressel:TypedPreferences:v2.1.1') {
        exclude module: 'app'
        transitive = true
    }

    // Material Dialogs
    api 'com.afollestad.material-dialogs:core:2.0.0-beta4'

    // RxJava (RxKotlin actually)
    api "io.reactivex.rxjava2:rxjava:2.2.0"
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'
    api "io.reactivex.rxjava2:rxkotlin:${rootProject.ext.rxKotlinVersion}"

    // RxBinding
    // RxJava binding APIs for Android UI widgets from the platform and support libraries.
    api "com.jakewharton.rxbinding2:rxbinding:${rootProject.ext.rxBindingVersion}"
    api "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${rootProject.ext.rxBindingVersion}"
    api "com.jakewharton.rxbinding2:rxbinding-design:${rootProject.ext.rxBindingVersion}"

    // RxLifecycle to prevent memory leaks
    def rxLifecycleVersion = rootProject.ext.rxLifecycleVersion
    api "com.trello.rxlifecycle2:rxlifecycle:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-android:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rxLifecycleVersion"

    // RxBus (event bus)
    api 'com.eightbitlab:rxbus:1.0.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}