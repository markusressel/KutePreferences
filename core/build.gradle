apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.jetbrains.dokka-android'

apply plugin: 'com.github.dcendents.android-maven'
group = rootProject.ext.group

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    lintOptions {
        abortOnError false
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    def markusresselCommonsVersion = "master-SNAPSHOT"
    // choose the ones you need
    api("com.github.markusressel.commons:commons_android_themes:${markusresselCommonsVersion}")
    api("com.github.markusressel.commons:commons_core:${markusresselCommonsVersion}")

    api "androidx.annotation:annotation:$androidKtxVersion"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.4.0-alpha02"
    implementation "androidx.core:core-ktx:1.3.2"

    implementation "com.google.code.gson:gson:2.8.6"
    implementation('com.github.markusressel:TypedPreferences:v2.1.1') {
        exclude module: 'app'
        transitive = true
    }

    // Android Architecture Components
    api "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Material Dialogs
    api 'com.afollestad.material-dialogs:core:3.3.0'

    // RxJava (RxKotlin actually)
    api "io.reactivex.rxjava2:rxjava:2.2.19"
    api 'io.reactivex.rxjava2:rxandroid:2.1.0'
    api "io.reactivex.rxjava2:rxkotlin:${rootProject.ext.rxKotlinVersion}"

    // RxBinding
    // RxJava binding APIs for Android UI widgets from the platform and support libraries.
    api "com.jakewharton.rxbinding2:rxbinding:${rootProject.ext.rxBindingVersion}"
    api "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${rootProject.ext.rxBindingVersion}"
    api "com.jakewharton.rxbinding2:rxbinding-design:${rootProject.ext.rxBindingVersion}"

    // RxLifecycle to prevent memory leaks
    def rxLifecycleVersion = rootProject.ext.rxLifecycleVersion
    api "com.trello.rxlifecycle2:rxlifecycle:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-android:$rxLifecycleVersion"
    api "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rxLifecycleVersion"

    // RxBus (event bus)
    api 'com.eightbitlab:rxbus:1.0.2'

    // Epoxy - Recyclerview for dummies
    api "com.airbnb.android:epoxy:$epoxyVersion"
    api "com.airbnb.android:epoxy-databinding:$epoxyVersion"
//    implementation "com.airbnb.android:epoxy-paging:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives sourcesJar
    archives dokkaJar
}